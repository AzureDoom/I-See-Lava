plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'maven-publish'
    id 'net.darkhax.curseforgegradle' version '1.+'
    id "com.modrinth.minotaur" version "2.+"
}

import com.modrinth.minotaur.dependencies.ModDependency
import groovy.json.JsonSlurper
import groovy.json.JsonOutput

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

base {
    archivesName = project.modName
}
version = project.modVersion
group = project.mavenGroup

repositories {

}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraftVersion}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loaderVersion}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabricVersion}"
}

processResources {
    inputs.property "version", project.modVersion

	filesMatching("fabric.mod.json") {
		expand "version": project.modVersion
	}
}

processResources {
	doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.modName}"}
	}
}

if (file('key.properties').exists()) {
    task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
        def curseProp = new Properties()
        File secretPropsFile = file("key.properties")
        curseProp.load(secretPropsFile.newInputStream())
        apiToken = curseProp.getProperty("curseKey")

        def mainFile = upload("543575", remapJar)
        mainFile.releaseType = "release"
        mainFile.changelogType = "text"
        mainFile.addRequirement "fabric-api"
        mainFile.changelog = file("CHANGELOG.txt")
        mainFile.addModLoader("Fabric", "Quilt")
        mainFile.addJavaVersion("Java 17")
        mainFile.addGameVersion("1.20.2")
    }
}

if (file('key.properties').exists()) {
	modrinth {
		def modrinthProp = new Properties()
		File secretPropsFile = file("key.properties")
		modrinthProp.load(secretPropsFile.newInputStream())

		token = modrinthProp.getProperty('modrinthKey')
		projectId = project.modrinthID
		versionNumber = project.modVersion
		uploadFile = remapJar
		changelog = rootProject.file("changelog.txt").text
		gameVersions = ['1.20.2']
		loaders = ['fabric']
		dependencies {
        	required.project "fabric-api"
   		}
	}
}
